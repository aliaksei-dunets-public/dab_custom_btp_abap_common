@Metadata.layer: #CORE

@Search.searchable: true
@UI: {
  headerInfo: {
    typeName: 'Variable',
    typeNamePlural: 'Variables',
    title: {
      type: #STANDARD,
      value: 'variable_id'
    },
    description: {
      type: #STANDARD,
      value: 'variable_name'
    }
  }
}

annotate entity ZDAB_C_VariableDefinition_M with
{
  @UI.facet : [
    {
      id:'MainCollectionFacet',
      type: #COLLECTION,
      purpose: #STANDARD,
      label: 'Variable',
      position: 10
    },
    {
      id: 'DetailData',
      parentId: 'MainCollectionFacet',
      purpose: #STANDARD,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'Detail_Data',
      label: 'Details',
      position: 10
    },
    {
      id: 'AdminData',
      parentId: 'MainCollectionFacet',
      purpose: #STANDARD,
      type: #IDENTIFICATION_REFERENCE,
      targetQualifier: 'Admin_Data',
      label: 'Admin Data',
      position: 20
    },
    {
      id: 'ParameterConfigTables',
      purpose: #STANDARD,
      type: #LINEITEM_REFERENCE,
      targetElement: '_ParameterConfig',
      label: 'Parameters',
      position: 20
    }
  ]

  @UI: {
    lineItem:       [{ position: 10, importance: #HIGH }],
    identification: [{ position: 10, qualifier: 'Detail_Data'}],
    selectionField: [{ position: 10 }]
  }
  @Search.defaultSearchElement: true
  variable_id;
  
  @UI: {
    lineItem: [{ position: 20, importance: #HIGH }],
    identification: [{ position: 20, qualifier: 'Detail_Data' }]
  }
  @Search.defaultSearchElement: true
  @EndUserText.label: 'Variable Name'
  variable_name;
  
  @UI: {
    lineItem:       [{ position: 30, importance: #HIGH }],
    identification: [{ position: 30, qualifier: 'Detail_Data' }],
    selectionField: [{ position: 30 }]
  }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZDAB_I_ClassName_VH', element: 'ABAPClassName'  } }]
  variable_class_handler;
  
  @UI: {
    lineItem:       [{ position: 40, importance: #MEDIUM }],
    identification: [{ position: 40, qualifier: 'Detail_Data' }]
  }
  is_vh_detected;

  @UI: {
    identification: [{ position: 10, qualifier: 'Admin_Data' }]
  }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZDAB_I_User_VH', element: 'UserID'  } }]
  created_by;

  @UI: {
    identification: [{ position: 20, qualifier: 'Admin_Data' }]
  }
  @EndUserText.label: 'Created At'
  created_at;

  @UI: {
    identification: [{ position: 30, qualifier: 'Admin_Data' }]
  }
  @Consumption.valueHelpDefinition: [{ entity : {name: 'ZDAB_I_User_VH', element: 'UserID'  } }]
  last_changed_by;

  @UI: {
    identification: [{ position: 40, qualifier: 'Admin_Data' }]
  }
  @EndUserText.label: 'Last Changed At'
  last_changed_at;

  @UI.hidden: true
  local_last_changed_by;
  @UI.hidden: true
  local_last_changed_at;
  @UI.hidden: true
  CreatedByDescr;
  @UI.hidden: true
  LastChangedByDescr;

}